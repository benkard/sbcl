(in-package "SB!EVAL2")


;;;; LAMBDA LIST PARSING FUNCTIONS
(defun lambda-binding-vars (entry kind)
  (check-type kind (member :aux :optional :key :required))
  (ecase kind
    ((:key :optional)
     (etypecase entry
       (cons   (list (etypecase (first entry)
                       (cons   (second (first entry)))
                       (symbol (first entry)))
                     (if (cddr entry)
                         (third entry)
                         (gensym))))
       (symbol (list entry (gensym)))))
    ((:required)
     (etypecase entry
       (cons   (list (first entry)))
       (symbol entry)))
    ((:aux)
     (etypecase entry
       (cons   (list (first entry)))
       (symbol entry)))))

(defun lambda-binding-main-var (entry)
  (etypecase entry
    (cons   (etypecase (first entry)
              (cons   (second (first entry)))
              (symbol (first entry))))
    (symbol entry)))

(defun lambda-binding-suppliedp-var (entry)
  (etypecase entry
    (cons (third entry))
    (symbol nil)))

(defun lambda-simple-binding-vars (entry)
  (etypecase entry
    (cons   (list (first entry)))
    (symbol (list entry))))

(defun lambda-binding-default (entry)
  (etypecase entry
    (cons   (second entry))
    (symbol nil)))

(defun lambda-key (entry)
  (flet ((keywordify (sym)
           (intern (symbol-name sym) "KEYWORD")))
    (etypecase entry
      (cons   (etypecase (first entry)
                (cons   (first (first entry)))
                (symbol (keywordify (first entry)))))
      (symbol (keywordify entry)))))


;;;; MISCELLANEOUS UTILITIES
(defun disjointp (list1 list2)
  (null (intersection list1 list2)))

(defmacro lazy-getf (plist indicator default)
  (let ((sentinel (gensym))
        (result (gensym "RESULT")))
    `(let* ((,result (getf ,plist ,indicator ',sentinel)))
       (if (eq ,result ',sentinel)
           ,default
           ,result))))

